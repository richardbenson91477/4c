@load "../model"
@load "../../win"
@load "../../colors"

main% @do
    %win_w 640
    %win_h 480

    .win_conf new^ :win_conf @or
        return 1

    .win_conf\%w %win_w
    .win_conf\%h %win_h
    .win_conf\$title "model_1"
    .win_conf\^gl true
 
    win_init^ @or
        return 2

    .win new^ :win .win_conf @or
        return 3

    .win win_open^ @or
        return 4

    .view new^ :view @or
        return 5
 
    .view set^ %win_w %win_h @or
        return 6
 
    .model new^ :model @or
        return 7

    .model load^ .view _ "test1.model" @or
        return 8

    .model_pos 0.0 0.0 -4.0
    .eye_v 0.0 0.0 4.0
    .light_v 0.0 0.0 0.0 0.0
    .model_r 0.0 0.0 0.0
 
    %frame_t (1.0 / %win_run_fps)
 
    %zr 0.0
    win_tick^ @while
        .view tick^ %frame_t @or
            break

        %zr (%zr + (%frame_t / 2.0))
        (%zr > %PI2) @and
            %zr (%zr - %PI2)

        .view render^

        .view mv_reset^

        .light_v[0] (.win\%m_xf * 4.0)
        .light_v[1] (.win\%m_yf * 4.0)
        .view light_setpos^ _light_v
 
        .view mv_negtrans^ .eye_v
        .view mv_push^
            .view mv_trans^ .model_pos

            .model_r[0] (.win\%m_yf * 1.8)
            .model_r[1] (.win\%m_xf * 1.8)
            .model_r[2] 0.0
            .view mv_rot^ .model_r

            .model_r[0] 0.0
            .model_r[1] 0.0
            .model_r[2] %zr
            .view mv_rot^ _model_r

            .model render^ .view
        .view mv_pop^

        .win render^
    @wend
 
    .model del^ .view
    .view del^

    win_deinit^

    .win_conf del^

    return 0
@done

